apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '29.0.3'

    defaultConfig {
        applicationId "com.serwylo.lexica"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        minSdkVersion 14
    }

    lintOptions {
        // The ranslations are crowd-sourced, and so wile likely never be 100% complete for all locales.
        disable 'MissingTranslation'

        // Due to getting the following error with Travis CI:
        //   /home/travis/build/lexica/lexica/app: Error: The SDK platform-tools version ((21)) is too old  to check APIs compiled with API 23; please update [NewApi]
        // Until the newer version of platform-tools is installed on their CI server, lets not consider it a failure.
        abortOnError false

        // Ouptuts report during build, so that it can be seen in the CI log.
        textReport true
    }

    buildTypes {
        all {
            minifyEnabled false
            shrinkResources false
        }
    }

    testOptions {
        // http://tools.android.com/tech-docs/unit-testing-support#TOC-Method-...-not-mocked.-
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

dependencies {
    implementation project(':libraries:trie')
    implementation project(':libraries:liblexica')
    implementation 'com.android.support:appcompat-v7:23.4.0'
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    testImplementation "junit:junit:4.12"
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}

afterEvaluate {
    assembleDebug.dependsOn rootProject.buildDictionaries
    if (tasks.findByPath('assembleRelease')) {
        assembleRelease.dependsOn rootProject.buildDictionaries
    }
}
